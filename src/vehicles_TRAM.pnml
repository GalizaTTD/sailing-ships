
/*
#    OpenTTD Galician Roads, Trams & Vehicles (GALVET) -  Copyright (C) 2021  Pablo Villaverde Castro
#    This program is free software: you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation, either version 3 of the License, or (at your option) any later version.
#    This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
#	 You should have received a copy of the GNU General Public License along with this program.  If not, see <https://github.com/pvillaverde/openttd-spanish-railsets/blob/main/LICENSE>.
*/

    sort(FEAT_ROADVEHS, [TRAM_CORU32]);  

	template tramCoruTemplate() {
		[   0,  0,   8,  18,  -2, -11]
		[   9,  0,  16,  14,  -8,  -6]
		[  26,  0,  22,  16, -11, -11]
		[  49,  0,  16,  17,  -2,  -7]
		[  66,  0,   8,  22,  -5, -12] 
		[  75,  0,  16,  17, -14,  -8]
		[  92,  0,  22,  16, -12, -11]
		[ 115,  0,  16,  14,  -8,  -9] 
	}
	template tramCoruPurchaseTemplate() { [  0, 20, 60, 15, -24, -8]}

	spriteset (tramCoru32, "src/gfx/vehicles/TRAM/coru32.gimp.png") { tramCoruTemplate()}
	spriteset (tramCoru27, "src/gfx/vehicles/TRAM/coru27.gimp.png") { tramCoruTemplate()}
	spriteset (tramCoruPurchase32, "src/gfx/vehicles/TRAM/coru32.gimp.png") { tramCoruPurchaseTemplate()}

	switch(FEAT_ROADVEHS, SELF, tramCoruSwitch, build_year % 2) {
		0: tramCoru32;
		1: tramCoru27;
	}
    item(FEAT_ROADVEHS, TRAM_CORU32) {

        property {
            // common properties 
            name:							string(STR_VEHICLES_TRAM_CORU32_NAME);
            climates_available:				ALL_CLIMATES;
            introduction_date :				date(1997,01,01);
            sprite_id:						SPRITE_ID_NEW_ROADVEH; //use custom sprites

            // Spec
            power:							203 hp;
            speed:							50 km/h;
            weight:							18 ton;
            length:							1;
			tractive_effort_coefficient:	0.6;
		//	air_drag_coefficient:			1;

            // model
            model_life:						VEHICLE_NEVER_EXPIRES;
            vehicle_life:					100;
            retire_early:					0;
            reliability_decay:				0;

            // cargo 
            cargo_capacity:					22;
            refittable_cargo_classes:		NO_CARGO_CLASS;
            non_refittable_cargo_classes:	NO_CARGO_CLASS;
            cargo_allow_refit:				[PASS];
            loading_speed:					2;

            // Costs   
            cost_factor:					20;
            running_cost_base:				RUNNING_COST_ROADVEH;
            running_cost_factor:			20;

            // Flags
			visual_effect:					VISUAL_EFFECT_ELECTRIC;
			sound_effect:					sound("src/wav/tram_bell.wav");
			tram_type:						ELRL;
            misc_flags:						bitmask(ROADVEH_FLAG_TRAM); //make this a tram
        }

        graphics {
            additional_text:			return string(STR_VEHICLES_TRAM_CORU32_DESC);
            default:					tramCoruSwitch;
            purchase:					tramCoruPurchase32;
        }
    }  